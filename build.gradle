import ch.so.agi.gretl.tasks.*
import ch.so.agi.gretl.api.*

apply plugin: 'ch.so.agi.gretl'

buildscript {
    repositories {
        maven { url "http://jars.interlis.ch" }
        maven { url "http://jars.umleditor.org" }
        maven { url "https://repo.osgeo.org/repository/release/" }
        maven { url "https://plugins.gradle.org/m2/" }
        mavenCentral()
    }
    dependencies {
        classpath group: 'ch.so.agi', name: 'gretl',  version: '2.2.+'
    }
}

defaultTasks 'importCsv'

def iliModelName = "SO_HBA_Grundstuecke_20230109"
def fileName = "20230110_cafm_Grundstuecke.csv"

task validateCsv(type: CsvValidator) {
    description = "Validiert die CSV-Datei"
    models = iliModelName
    firstLineIsHeader = true
    encoding = "ISO-8859-1"
    valueSeparator = ";"
    valueDelimiter = null
    dataFiles = [fileName]
}

task truncateTable(type: SqlExecutor, dependsOn: 'validateCsv') {
    description = "Leert die Tabelle grundstuecke_grundstueck"
    database = [dbUriEdit, dbUserEdit, dbPwdEdit]
    sqlFiles = [
            'truncate_table.sql'
    ]
}

task importCsv(type: CsvImport, dependsOn: 'truncateTable') {
    description = "Import die CVS-Datei in die Datenbank"
    database = [dbUriEdit, dbUserEdit, dbPwdEdit]
    schemaName = "hba_grundstuecke_v1"
    tableName = "grundstuecke_grundstueck"
    firstLineIsHeader=true
    encoding = "ISO-8859-1"
    valueSeparator = ";"
    valueDelimiter = null
    dataFile = fileName
}

task transferToPub(type: Db2Db) {
    description = "Publiziert in die Pub-DB"
    sourceDb = [dbUriEdit, dbUserEdit, dbPwdEdit]
    targetDb = [dbUriPub, dbUserPub, dbPwdPub]
    transferSets = [
            new TransferSet('transfer_grundstuecke.sql', 'hba_grundstuecke_pub_v1.grundstuecke_liegenschaft', true)
    ];
}